INCLUDE (StringToInternalBool)
MACRO (FINDVACOPY)
  #
  # Dependencies
  #
  STRINGTOINTERNALBOOL(_HAVE_STDLIB_H HAVE_STDLIB_H)
  STRINGTOINTERNALBOOL(_HAVE_STDARG_H HAVE_STDARG_H)
  #
  # Test
  #
  FOREACH (KEYWORD "va_copy" "_va_copy" "__va_copy")
    MESSAGE(STATUS "Looking for ${KEYWORD}")
    TRY_COMPILE (C_HAS_${KEYWORD} ${CMAKE_CURRENT_BINARY_DIR}
      ${CMAKE_CURRENT_SOURCE_DIR}/cmake/va_copy.c
      COMPILE_DEFINITIONS "-DC_VA_COPY=${KEYWORD} -DHAVE_STDLIB_H=${_HAVE_STDLIB_H} -DHAVE_STDARG_H=${_HAVE_STDARG_H}")
    IF (C_HAS_${KEYWORD})
      MESSAGE(STATUS "Looking for ${KEYWORD} - found")
      SET (C_VA_COPY ${KEYWORD})
      BREAK ()
    ENDIF ()
  ENDFOREACH ()
ENDMACRO ()
IF (NOT C_VA_COPY)
  FINDVACOPY ()
  SET (C_VA_COPY "${C_VA_COPY}" CACHE STRING "C VA_COPY")
  MARK_AS_ADVANCED (C_VA_COPY)
ENDIF (NOT C_VA_COPY)
